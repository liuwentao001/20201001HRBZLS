CREATE OR REPLACE PROCEDURE HRBZLS.SP_OPERROLEFUNCLOG(V_RID    IN VARCHAR2,
                                               V_VERID  IN NUMBER,
                                               V_OPERID IN VARCHAR2,
                                               V_WS     IN VARCHAR2,
                                               V_FUNCID IN VARCHAR2) AS
  CURSOR C_ADD IS
    SELECT ORFRID, ORFFID,orfstartdate , ORFEXPIRYDATE
      FROM OPERROLEFUNC_EX T
     WHERE ORFRID = V_RID
    MINUS
    SELECT ORFRID, ORFFID,orfstartdate , ORFEXPIRYDATE
      FROM OPERROLEFUNC_EXHIS T
     WHERE ORFRID = V_RID
       AND VERID = V_VERID;

  CURSOR C_DELETE IS
    SELECT ORFRID, ORFFID, orfstartdate ,ORFEXPIRYDATE
      FROM OPERROLEFUNC_EXHIS T
     WHERE ORFRID = V_RID
       AND VERID = V_VERID
    MINUS
    SELECT ORFRID, ORFFID, orfstartdate ,ORFEXPIRYDATE
      FROM OPERROLEFUNC_EX T
     WHERE ORFRID = V_RID;
  VHIS   OPERROLEFUNC_EXHIS%ROWTYPE;
  V_DESC VARCHAR2(1000);
BEGIN

  VHIS := NULL;
  OPEN C_ADD;
  LOOP
    FETCH C_ADD
      INTO VHIS.ORFRID, VHIS.ORFFID, VHIS.ORFSTARTDATE, VHIS.ORFEXPIRYDATE;
    EXIT WHEN C_ADD%NOTFOUND OR C_ADD%NOTFOUND IS NULL;
    V_DESC := '赋予角色[' || VHIS.ORFRID || ']' || FGETFUNCNAME(VHIS.ORFFID) ||
              '功能，有效期限'|| TO_CHAR(VHIS.ORFSTARTDATE, 'YYYY-MM-DD')||'至' || TO_CHAR(VHIS.ORFEXPIRYDATE, 'YYYY-MM-DD');
    SP_OPERLOG(V_OPERID, V_WS, V_FUNCID, V_DESC);
  END LOOP;
  CLOSE C_ADD;

  VHIS := NULL;
  OPEN C_DELETE;
  LOOP
    FETCH C_DELETE
      INTO VHIS.ORFRID, VHIS.ORFFID, VHIS.ORFSTARTDATE, VHIS.ORFEXPIRYDATE;
    EXIT WHEN C_DELETE%NOTFOUND OR C_DELETE%NOTFOUND IS NULL;
    V_DESC := '收回角色[' || VHIS.ORFRID || ']' || FGETFUNCNAME(VHIS.ORFFID) || '功能';
    SP_OPERLOG(V_OPERID, V_WS, V_FUNCID, V_DESC);
  END LOOP;
  CLOSE C_DELETE;

EXCEPTION
  WHEN OTHERS THEN
    IF C_ADD%ISOPEN THEN
      CLOSE C_ADD;
    END IF;
    IF C_DELETE%ISOPEN THEN
      CLOSE C_DELETE;
    END IF;
    ROLLBACK;
    RAISE;
END;
/

