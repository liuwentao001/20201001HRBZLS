CREATE OR REPLACE PROCEDURE HRBZLS.SP_收回过期权限 IS
  CURSOR C_DEL1 IS
    SELECT ORFRID, ORFFID
      FROM OPERROLEFUNC_EX
     WHERE ORFEXPIRYDATE < TRUNC(SYSDATE);

  CURSOR C_DEL2 IS
    SELECT ORFMRID, ORFMFID, ORFMETHOD
      FROM OPERRFMETHOD_EX
     WHERE ORFMEXPIRYDATE < TRUNC(SYSDATE);

  CURSOR C_DEL3 IS
    SELECT ORFOAID, ORFFID, ORFTYPE
      FROM OPERACCNTROLEFUNC_EX
     WHERE ORFEXPIRYDATE < TRUNC(SYSDATE);

  CURSOR C_DEL4 IS
    SELECT ORFMFID, ORFMETHOD, ORFMRID, ORFMTYPE
      FROM OPERACCNTRFMETHOD_EX
     WHERE ORFMEXPIRYDATE < TRUNC(SYSDATE);

  DEL1   OPERROLEFUNC_EX%ROWTYPE;
  DEL2   OPERRFMETHOD_EX%ROWTYPE;
  DEL3   OPERACCNTROLEFUNC_EX%ROWTYPE;
  DEL4   OPERACCNTRFMETHOD_EX%ROWTYPE;
  V_DESC VARCHAR2(1000);
BEGIN
  --删除角色功能
  DEL1 := NULL;
  OPEN C_DEL1;
  LOOP
    FETCH C_DEL1
      INTO DEL1.ORFRID, DEL1.ORFFID;
    EXIT WHEN C_DEL1%NOTFOUND OR C_DEL1%NOTFOUND IS NULL;
    V_DESC := '系统自动收回角色[' || DEL1.ORFRID || ']的过期功能' ||
              FGETFUNCNAME(DEL1.ORFFID);
    SP_OPERLOG('systerm', null, null, V_DESC);
    DELETE FROM OPERROLEFUNC
     WHERE ORFRID = DEL1.ORFRID
       AND ORFFID = DEL1.ORFFID;
  END LOOP;
  CLOSE C_DEL1;

  --删除角色功能方法
  DEL2 := NULL;
  OPEN C_DEL2;
  LOOP
    FETCH C_DEL2
      INTO DEL2.ORFMRID, DEL2.ORFMFID, DEL2.ORFMETHOD;
    EXIT WHEN C_DEL2%NOTFOUND OR C_DEL2%NOTFOUND IS NULL;
    V_DESC := '系统自动收回角色[' || DEL2.ORFMRID || ']' ||
              FGETFUNCNAME(DEL2.ORFMFID) || '功能的过期方法' ||
              FGETFUNCMETHODNAME(DEL2.ORFMFID, DEL2.ORFMETHOD);
    SP_OPERLOG('systerm', null, null, V_DESC);
    DELETE FROM OPERRFMETHOD
     WHERE ORFMRID = DEL2.ORFMRID
       AND ORFMFID = DEL2.ORFMFID
       AND ORFMETHOD = DEL2.ORFMETHOD;
  END LOOP;
  CLOSE C_DEL2;

  --删除操作员自定义功能
  DEL3 := NULL;
  OPEN C_DEL3;
  LOOP
    FETCH C_DEL3
      INTO DEL3.ORFOAID, DEL3.ORFFID, DEL3.ORFTYPE;
    EXIT WHEN C_DEL3%NOTFOUND OR C_DEL3%NOTFOUND IS NULL;
    V_DESC := '系统自动收回操作员[' || FGETOPERNAME(DEL3.ORFOAID) || '[' ||
              DEL3.ORFOAID || ']的过期功能' || FGETFUNCNAME(DEL3.ORFFID);
    SP_OPERLOG('systerm', null, null, V_DESC);
    DELETE FROM OPERACCNTROLEFUNC
     WHERE ORFOAID = DEL3.ORFOAID
       AND ORFFID = DEL3.ORFFID
       AND ORFTYPE = DEL3.ORFTYPE;
  END LOOP;
  CLOSE C_DEL3;

  --删除操作员自定义功能方法
  DEL4 := NULL;
  OPEN C_DEL4;
  LOOP
    FETCH C_DEL4
      INTO DEL4.ORFMFID, DEL4.ORFMETHOD, DEL4.ORFMRID, DEL4.ORFMTYPE;
    EXIT WHEN C_DEL4%NOTFOUND OR C_DEL4%NOTFOUND IS NULL;
    V_DESC := '系统自动收回操作员[' || FGETOPERNAME(DEL4.ORFMRID) || '[' ||
              DEL4.ORFMRID || ']' || FGETFUNCNAME(DEL4.ORFMFID) ||
              '功能的过期方法' || FGETFUNCMETHODNAME(DEL4.ORFMFID, DEL4.ORFMETHOD);
    SP_OPERLOG('systerm', null, null, V_DESC);
    DELETE FROM OPERACCNTRFMETHOD
     WHERE ORFMFID = DEL4.ORFMFID
       AND ORFMETHOD = DEL4.ORFMETHOD
       AND ORFMRID = DEL4.ORFMRID
       AND ORFMTYPE = DEL4.ORFMTYPE;
  END LOOP;
  CLOSE C_DEL4;
  
  --统一提交
  COMMIT;

EXCEPTION
  WHEN OTHERS THEN
    IF C_DEL1%ISOPEN THEN
      CLOSE C_DEL1;
    END IF;
    IF C_DEL2%ISOPEN THEN
      CLOSE C_DEL2;
    END IF;
    IF C_DEL3%ISOPEN THEN
      CLOSE C_DEL3;
    END IF;
    IF C_DEL4%ISOPEN THEN
      CLOSE C_DEL4;
    END IF;
    ROLLBACK;
    RAISE;
END SP_收回过期权限;
/

